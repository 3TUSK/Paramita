plugins {
    id 'fabric-loom' version '0.12-SNAPSHOT'
    id 'io.github.juuxel.loom-quiltflower' version '1.7.1'
}

platformInfo {
    minecraftVersion = '1.14.4'
    platform = 'Fabric'
}

loom {
    // Uncomment this to enable the usage of Access Widener
    // accessWidenerPath = file('src/main/resources/examplemod.accesswidener')

    runs {
        client {
            runDir './run-client'
        }

        server {
            runDir './run-server'
        }

        datagen {
            client()
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.modid=examplemod"
            vmArg "-Dfabric-api.datagen.output-dir=${file("./src/generated/resources")}"
        }

        configureEach {
            ideConfigGenerated(true)
        }
    }
}

repositories {
    maven {
        name = "ParchmentMC"
        url = "https://maven.parchmentmc.net/"
    }
    maven {
        // ModMenu
        name = 'TerraformerMC'
        url = 'https://maven.terraformersmc.com/releases/'
    }
}

dependencies {
    minecraft "com.mojang:minecraft:1.14.4"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:0.14.9"
    modImplementation "net.fabricmc.fabric-api:fabric-api:0.23.0+build.264-1.14"
    compileOnly project(":1.14.4-base")

    // Need this to tell me what mods are present at runtime
    // TODO This version does not seem to be available in TerraformerMC's maven repo
    // modRuntimeOnly group: 'com.terraformersmc', name: 'modmenu', version: '1.7.17'
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 8
}
