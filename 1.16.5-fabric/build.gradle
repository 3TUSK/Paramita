plugins {
    id 'fabric-loom' version '0.12-SNAPSHOT'
    id 'io.github.juuxel.loom-quiltflower' version '1.7.1'
}

platformInfo {
    minecraftVersion = '1.16.5'
    platform = 'Fabric'
    supportedMinecraftVersions = [ '1.16', '1.16.1', '1.16.2', '1.16.3', '1.16.4', '1.16.5' ]
}

loom {
    // Uncomment this to enable the usage of Access Widener
    // accessWidenerPath = file('src/main/resources/examplemod.accesswidener')

    runs {
        client {
            runDir './run-client'
        }

        server {
            runDir './run-server'
        }

        datagen {
            client()
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.modid=examplemod"
            vmArg "-Dfabric-api.datagen.output-dir=${file("./src/generated/resources")}"
        }

        configureEach {
            ideConfigGenerated(true)
        }
    }
}

repositories {
    maven {
        name = "ParchmentMC"
        url = "https://maven.parchmentmc.net/"
    }
    maven {
        // ModMenu
        name = 'TerraformerMC'
        url = 'https://maven.terraformersmc.com/releases/'
    }
}

dependencies {
    minecraft "com.mojang:minecraft:1.16.5"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-1.16.5:2022.03.06@zip")
    }
    modImplementation "net.fabricmc:fabric-loader:0.14.9"
    modImplementation "net.fabricmc.fabric-api:fabric-api:0.42.0+1.16"
    compileOnly project(":1.16.5-base")

    // Need this to tell me what mods are present at runtime
    modRuntimeOnly group: 'com.terraformersmc', name: 'modmenu', version: '1.16.23'
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 8
}
