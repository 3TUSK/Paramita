plugins {
    id 'fabric-loom' version '0.12-SNAPSHOT'
    id 'io.github.juuxel.loom-quiltflower' version '1.7.1'
}

platformInfo {
    minecraftVersion = '1.17.1'
    platform = 'Fabric'
}

loom {
    // Uncomment this to enable the usage of Access Widener
    // accessWidenerPath = file('src/main/resources/examplemod.accesswidener')

    runs {
        client {
            runDir './run-client'
        }

        server {
            runDir './run-server'
        }

        // TODO Check if game test is also available on 1.17
        gameTest {
            server()
            vmArg "-Dfabric-api.gametest=1"
        }

        datagen {
            client()
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.modid=examplemod"
            vmArg "-Dfabric-api.datagen.output-dir=${file("./src/generated/resources")}"
        }

        configureEach {
            ideConfigGenerated(true)
        }
    }
}

repositories {
    maven {
        name = "ParchmentMC"
        url = "https://maven.parchmentmc.net/"
    }
    maven {
        // ModMenu
        name = 'TerraformerMC'
        url = 'https://maven.terraformersmc.com/releases/'
    }
}

dependencies {
    minecraft "com.mojang:minecraft:1.17.1"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-1.17.1:2021.12.12@zip")
    }
    modImplementation "net.fabricmc:fabric-loader:0.14.9"
    modImplementation "net.fabricmc.fabric-api:fabric-api:0.46.1+1.17"
    compileOnly project(":1.17.1-base")

    // Need this to tell me what mods are present at runtime
    modRuntimeOnly group: 'com.terraformersmc', name: 'modmenu', version: '2.0.17'
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 16
}
