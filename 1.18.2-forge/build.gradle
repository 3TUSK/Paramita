plugins {
    id 'eclipse'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '5.1.+'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
    id 'org.spongepowered.mixin' version '0.7.+'
}

platformInfo {
    minecraftVersion = '1.18.2'
    platform = 'Forge'
    supportedMinecraftVersions = [ '1.18', '1.18.1', '1.18.2' ]
}

mixin {
    add sourceSets.main, "examplemod.refmap.json"

    config "examplemod.mixins.json"
}

repositories {
}

minecraft {
    mappings channel: 'official', version: '1.18.2'

    runs {
        client {
            workingDirectory file('./run-client')
            property 'forge.logging.console.level', 'info'
            property 'fml.earlyprogresswindow', 'false'
            ideaModule "${rootProject.name}.${project.name}.main"
            // Uncomment this line to change player name.
            // For ForgeGradle only, any '#' in username will be replaced with random digit.
            // args '--username', 'SomePlayer'
            mods {
                examplemod {
                    source sourceSets.main
                    source project(":1.18.2-base").sourceSets.main
                }
            }
        }

        server {
            workingDirectory file('./run-server')
            property 'forge.logging.console.level', 'info'
            property 'fml.earlyprogresswindow', 'false'
            ideaModule "${rootProject.name}.${project.name}.main"
            mods {
                examplemod {
                    source sourceSets.main
                    source project(":1.18.2-base").sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('../run')
            property 'fml.earlyprogresswindow', 'false'
            ideaModule "${rootProject.name}.${project.name}.main"
            // Uncomment this line to get your Mixin applied in Data-Gen environments
            // args "-mixin.config=examplemod.mixins.json"
            forceExit false
            mods {
                examplemod {
                    source sourceSets.main
                    source project(":1.18.2-base").sourceSets.main
                }
            }
            args '--mod', 'examplemod', '--all', '--output', '"' + file('src/generated/resources/') + '"'
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:1.18.2-40.2.1"
    compileOnly project(":1.18.2-base")
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

jar {
    finalizedBy 'reobfJar'
}
