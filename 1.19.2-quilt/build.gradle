// The example mod template provided by Quilt uses a less-known Gradle feature
// called Version Catalogs, documented here:
// https://docs.gradle.org/7.4/userguide/platforms.html#sec:plugins
// For now we converted them back to the normal way to declare dependency versions.
// In the future we might want to migrate all of them over.

plugins {
    id 'maven-publish'
    id 'org.quiltmc.loom' version '1.0.+'
}

platformInfo {
    minecraftVersion = '1.19.2'
    platform = 'Quilt'
    supportedMinecraftVersions = [ '1.19', '1.19.1', '1.19.2' ]
}

loom {
    // Uncomment this to enable the usage of Access Widener
    // accessWidenerPath = file('src/main/resources/examplemod.accesswidener')

    runs {
        client {
            runDir './run-client'
        }

        server {
            runDir './run-server'
        }

        // TODO Datagen

        configureEach {
            ideConfigGenerated(true)
        }
    }
}

repositories {
    maven {
        // ModMenu
        name = 'TerraformerMC'
        url = 'https://maven.terraformersmc.com/releases/'
    }
}

dependencies {
    minecraft 'com.mojang:minecraft:1.19.2'
    mappings loom.layered {
        mappings "org.quiltmc:quilt-mappings:1.19.2+build.21:intermediary-v2"
        officialMojangMappings()
    }
    modImplementation 'org.quiltmc:quilt-loader:0.17.6'
    modImplementation 'org.quiltmc.quilted-fabric-api:quilted-fabric-api:4.0.0-beta.21+0.67.0-1.19.2'
    compileOnly project(":1.19.2-base")

    // Need this to tell me what mods are present at runtime
    modRuntimeOnly group: 'com.terraformersmc', name: 'modmenu', version: '4.1.2'
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

jar {
    filesMatching('LICENSE') {
        rename '^(LICENSE.*?)(\\..*)?$', "\$1_${archivesBaseName}\$2"
    }
}